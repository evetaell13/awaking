label cafe1:
    nar "Катю я сперва не увидела, но она приподнялсь со своего места и помахала мне рукой."
    nar "Я улыбнулась, кивнула и тут только заметила, что рядом с подругой сидит какой-то незнакомый парень."
    alina "\"Каштановые волосы, стильные очки... Что-то не припомню его на вечеринке.\""
    kate "Алин, привет! Давай, садись к нам. Я тебе твой любимый латте заказала."
    nar "Подруга суетливо указала на свободный стул и расплылась в слегка смущенной улыбке, переглядываясь с парнем."
    nar "Судя по ее приветствию, она не заметила мой третий глаз."
    if uh >= 15:
        nar "Да и щупальце, растущее у меня прямо из спины, её не смущало."
    kate "Знакомься, это Артём — крутой фотограф. А еще он мой парень."
    nar "Подружка не без гордости улыбнулась."
    artem "Привет! Подписывайся на мою инсту."
    alina "Приятно познакомиться. Катя мне о тебе все уши прожужжала."
    kate "Алина!"
    alina "Да я пошутила."
    kate "Видишь, не зря мы с тобой на ту вечеринку пошли."
    alina "Вот только я что-то не помню там Артёма."
    kate "А он позже пришел. Мы с ним в коридоре столкнулись. Ну как столкнулись, я свой коктейль на него пролила. Но он не обиделся."
    artem "А потом я уговорил Катю поехать ко мне и помочь мне с выбором фотографий для городской выставки. Ты уж извини, что я ее утащил тогда с собой."
    kate "Да, нехорошо получилось."
    alina "Да уж ладно, проехали. Много у тебя подписчиков?"
    artem "Пара тысяч. Но я пока только раскручиваюсь."
    alina "Ясно."
    kate "Нет, у него реально классные фотки. Ты должна их заценить. Артём, покажи ей те, которые мы отобрали."
    nar "Парень кивнул и уткнулся в телефон."
    kate "А у тебя как оно?"
    menu:
        nar "Подруга выглядела такой счастливой, что я не была уверена, что хочу грузить её своими проблемами."
        "Сказать, что всё в порядке":
            alina "Да я что-то вчера не выспалась, вот и мерещится всякая чушь в зеркале. Думаю, всё будет в порядке."
            nar "Катя сочувственно кивнула и уже открыла рот, чтобы что-то ответить, как Артём, наконец, подал голос."
        "Рассказать о проблемах":
            $od = od + 5
            alina "Знаешь, мне кажется, у меня с головой не в порядке."
            kate "С чего ты взяла?"
            alina "Я сегодня в зеркале увидела какое-то чудовище вместо себя."
            kate "Может, сзади стояла Аврора Говардовна?"
            alina "Вот ты шутишь, а у меня на лбу был третий глаз!"
            if uh >= 15:
                alina "И щупальце из спины росло."
            kate "Звучит крипово. Может, это тебе приснилось? Тебе старуха ничего там в кофе не подлила?"
            alina "Я не..."
            nar "Наш разговор прервал Артём."
    artem "Вот, кажись нашёл нужные. Зацени."
    nar "Парень протянул мне руку с телефоном."
    nar "Я подала ладонь в ответ, и тут невольно заметила, как задравшийся рукав его рубашки открыл странные отметины на запястье моего нового знакомого."
    nar "Неожиданно мой лоб пронзил укол боли, и прямо на глазах шрамы на его руке превращались в открытые раны с ровными краями, с них по его узкому запястью полилась на пол кровь."
    menu: #todo ТАЙМЕР
        nar "Я в ужасе смотрела на эту картину, чувствуя, как лоб вновь пронзает раскаленной иглой вспышка боли..."
        "Закрыть лоб рукой":
            $glaz=0
            nar "Я с усилием прижала ладонь ко лбу и надавила. Боль понемногу стала проходить, сменяясь легким неприятным покалыванием."
        "Не закрывать":
            $glaz=1
            $uh = uh + 5
            nar "Я не могла заставить себя не смотреть."
            nar "И то, что я увидела, было ужасным. Артем, сжавшийся в комок на тусклом кафеле, в одной руке - лезвие бритвы, в другом - фотография светловолосой девушки."
            nar "Плечи парня ходят ходуном, он сотрясается от сдавленных рыданий. Еще секунда и на его руки, на фотографию, на кафель брызжет густая темная кровь."
            nar "Я охнула и дернула головой. Видение исчезло."
    nar "Катя и ее парень смотрели на меня с испугом и подозрением. Чтобы хоть как-то замять ситуацию, я выпалила то, что первым пришло мне в голову."
    alina "Артём, ты что, вены резал?"
    nar "Парень дернулся, словно я не спросила его, а ударила, а Катя нахмурилась и закусила губу."
    nar "Она хотела сказать что-то, но Артем жестом остановил ее и вперился в мое лицо сердитым взглядом."
    jump cafe2

label cafe2:
    menu:
        artem "А тебе какое дело?"
        "Рассказать о видении" if glaz==1:
            alina "Я... понимаешь, только ты не подумай, что я сумасшедшая. Мне видение было."
            alina "Ну... я увидела, когда ты мне протянул руку, на твоем запястье раны и кровь, много крови, ты сидел на кафельном полу и держал в руках фотографию светловолосой девушки..."
            alina "Я не понимаю, откуда это, я ведь тебя совсем не знаю, и эти видения, я не могу их контролировать. Прости, я не хотела показаться бестактной."
            call history_artem
        "Рассказать о видении" if glaz==0:
            alina "Я... понимаешь, только ты не подумай, что я сумасшедшая. Мне видение было."
            alina "Ну... я увидела, когда ты мне протянул руку, на твоем запястье раны и кровь, много крови..."
            alina "Я не понимаю, откуда это, я ведь тебя совсем не знаю, и эти видения, я не могу их контролировать. Прости, я не хотела показаться бестактной."
            call history_artem
        "Не рассказывать":
            $od = od - 5
            nar "Я замешкалась и потерла лоб."
            alina "Да вот, заметила у тебя шрамы на запястье и решила спросить."
            kate "Алинка, блин!"
            nar "Подруга укоризненно фыркнула, а ее парень вскинул рюкзак на плечо и принялся преувеличенно поспешно собираться."
            artem "Совсем забыл, мне срочно надо фотки отретушировать для каталога. Кать, увидимся."
            nar "На меня парень даже не взглянул. Я пожала плечами и посмотрела на подругу. Та хотела было броситься вслед за Артёмом, но все же пересилила себя и нахмурилась."
            kate "Алин, ну ты ваще, ты что, с ума сошла, такое спрашивать? Умеешь же найти тему!"
            alina "Извини..."
            kate "Что с тобой  в последнее время такое?"
            kate "Знаешь, мне кажется, тебе не помешает помощь специалиста. Ты не подумай чего, я о тебе забочусь. Ты нервная какая-то стала, стресс, универ, все понятно."
            kate "Вот, у меня тут визитка есть классного спеца. Мне Артем дал, я сама хотела походить к нему, тоже уже срываюсь по пустякам. На, в общем. Артем сказал, что человек в своей области очень грамотный."
            alina "Но..."
            nar "Не слушая моих возражений, подружка запихнула визитку мне в карман и, наскоро попрощавшись, побежала догонять своего Артема."
    jump cafe3



label history_artem:
    $zn = zn + 1
    nar "После моей сбивчивой речи, Катя с шумом втянула воздух, а Артем наклонился над столом и пристально уставился на меня, словно ища в моем взгляде признаки того, что я вру."
    nar "Наконец, он откинулся, привалившись спиной к стулу и вздохнул."
    artem "У меня девушка была. Настя... Мы с ней долго встречались, пожениться хотели."
    nar "Парень говорил медленно, словно через силу цедя слова. Катя ободряюще сжала его руку, лежавшую на столе."
    nar "Видимо, она уже знала эту историю."
    kate "Артём, может, не стоит..."
    nar "Но парень только мотнул головой и продолжал рассказывать глухим монотонным голосом."
    artem "С ней начало происходить что-то. Сама не своя она стала."
    artem "Говорила, что ее зовет что-то неведомое, а она не может противиться этому зову. Мы часто с ней стали ссориться, и она убегала."
    artem "Я всегда возвращал ее... находил около канала. Она стояла и в воду смотрела задумчиво так. А однажды я не пошел за ней, поругались мы особенно сильно и..."
    artem "В общем, бросилась она с моста в воду. Спасти не смогли. Даже тела не нашли, хотя долго потом искали."
    nar "Артем перевел дух и, высвободив руку из Катиной ладошки, принялся барабанить пальцами по столешнице."
    artem "Я себя винил в ее смерти, жить не хотелось, ну я и решил уйти вслед за Настей, дома вены порезал..."
    artem "Меня конечно, мать нашла, сразу в крик, скорую вызвала, успели откачать. Потом заставила к психотерапевту ходить. Он и помог мне это все пережить."
    nar "Я с сочувствием кивнула головой. Катя шмыгнула носом и вновь накрыла руку Артема своей ладонью."
    kate "Алинка, блин, умеешь же ты найти тему."
    kate " Знаешь что, тебе тоже не помешает психотерапевту показаться, ты не подумай чего, но все эти твои видения. Звучит как-то жутковато."
    kate "Артем, у тебя не сохранились контакты?"
    nar "Парень полез в свой рюкзак и, порывшись в нем, достал оттуда небольшой прямоугольник бумаги, видимо, визитку, и отдал мне."
    artem "Вот, Лозовский Лев Андреевич. Он меня к жизни вернул. Скажи, что от меня, и он тебе скидку сделает на прием... Он, правда, очень грамотный мужик."
    nar "Не желая обижать Катю и Артема, я угукнула и сунула визитку в карман."
    nar "Но после всех этих печальных откровений разговор не особо клеился, мы выпили по стакану сока, болтая о каких-то нейтральных пустяках, а потом Катя и ее парень, сославшись на неотложные дела, оставили меня одну."
    return



label cafe3:
    nar "Домой идти не хотелось, да и настроение после всего увиденного было препаршивым, и поэтому я решила развеяться."
    nar "Я полдня шаталась по городу, смотрела на низкое питерское небо, на крыши домов, кормила голубей, даже пыталась делать зарисовки в скетчбуке, который всегда таскала с собой в сумке."
    nar "Но толку из этого все равно не вышло. Линии получались рваными и нервными, раздерганными, как мое сегодняшнее состояние. Окончательно устав, я засобиралась домой."
    nar "Я открыла дверь квартиры и в нос мне ударила знакомая смесь запахов табака и кофе."
    nar "К горлу подкатила тошнота."
    nar "Я неуклюже попыталась повесить сумку, но дрожащими озябшими руками промахнулась мимо крючка. Содержимое сумки разлетелось по прихожей и на шум из комнаты вышла хозяйка с неизменной сигаретой в одной руке и чашкой кофе - в другой."
    nar "Она пристально посмотрела на меня, а потом медленно подняла с пола визитку психотерапевта."
    ag "Психотерапевт? Пфф, ненавижу психотерапевтов."
    ag "Никакого от них проку. Cначала он будет прикидываться милым и уговаривать пить его чертовы таблетки. А если откажешься - упечет в дурку. Уж я то знаю, поверь мне. Они хитрые, эти психотерапевты."
    ag "По глазам видно, что знают правду, но будут убеждать, что тебе просто мерещится все..."
    nar "Бабка с неохотой отдала мне визитку."
    ag "Но кто я такая, чтобы говорить что тебе делать и куда идти. Зачем вообще слушать сумасшедшую старуху."
    ag "Не смотри такими круглыми глазами, деточка. Конечно, я - сумашедшая. У меня и справка есть. Я её всегда с собой беру, когда иду ругаться в ЖЭК."
    nar "Бабка, посмеиваясь над моей реакцией, ушла к себе в комнату, а я скорее собрала всё с пола и шмыгнула в свою."
