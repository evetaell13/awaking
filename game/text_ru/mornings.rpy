
#=======================================================================УТРО-1==========================================================

label morning1_1:
    scene bathroom with fade
    $uh = uh + 5
    nar "На часах семь утра, а за окном - вечные питерские сумерки. Свет лампочки в ванной неестественно яркий. Скоро ехать в университет, ложиться уже смысла нет."
    nar "Да и сна ни в одном глазу после ночных приключений."
    alina_m "Что это у меня на лбу. Только этого не хватало. Как будто синяк, но откуда... Не помню, чтобы я падала или ударялась."
    alina_m "На ощупь холодный. Странно..."
    nar "Стук в дверь прервал мои размышления."
    ag "Деточка, ты там скоро?"
    alina "Уже выхожу, Аврора Говардовна, уже выхожу."
    nar "Это была хозяйка квартиры. Немного безумная женщина с мутным прошлым."
    nar "Аврора Говардовна курила дешевые сигареты и в любое время суток пила кофе из гэдээровской фарфоровой чашки, аристократично отставляя мизинец."
    nar "Я быстро замазала синяк тоналкой и вышла из ванной."
    #todo фон коридор
    alina "Простите. Я вас разбудила. Опоздала на развод мостов, пришлось остаться на Васильевском."
    ag "Надеюсь, хоть вечеринка была стоящей. Кхе-кхе. Ты же вроде на вечеринку с университетской подругой собиралась?"
    alina "Да..."
    nar "Старуха любила совать свой нос в чужие дела, и мне приходилось с этим мириться. Она брала с меня за комнату очень дешево для центра Питера, на другое такое жилье мне бы денег не хватило."
    ag "О пятне не переживай. Его никто не заметит. Пока что."
    alina "Что?"
    nar "Бросив на меня многозначительный взгляд, старуха скрылась за дверью в ванной."
    scene room_gg with fade
    nar "Я поспешила в свою комнату. Пора было собираться в универ."
    nar "В кармане запиликал телефон."
    alina_m "Наверное, это Катя объявилась."
    alina_m "Интересно, она хотя бы извинится? Хотя она даже не знает, что я вчера не успела домой."
    nar "Я открыла мессенджер. Так и есть. Сообщение было от Кати."
    k_nvl "Привет, ты как? Как вчера добралась?"
    nar "Я попыталась во всех подробностях вспомнить вчерашний вечер."
    scene room_gg_effect1 with Dissolve(1.0)
    scene room_gg_effect2 with Dissolve(1.0)
    scene room_gg_night with Dissolve(1.0)
    nar "Мы с Катей сидели здесь, в моей комнате, и она уговаривала меня пойти с ней на вечеринку к ее универским друзьям."
    alina "Что-то я не уверена, что хочу идти."
    kate "Да перестань, Алин, ну как старая бабка прям."
    alina "Тихо ты, Аврору Говардовну разбудишь!"
    kate "Пфф..."
    nar "Подруга фыркнула и покосилась на дверь. Но все было спокойно."
    kate "Так и просидишь весь курс серой мышкой в углу. А молодость дана, чтобы веселиться! Ну, не будь букой, пойдем, а? Будет весело."
    nar "От радостного предвкушения щеки Кати зарделись румянцем. Вот уж кому косметика никогда особо не была нужна с ее яркой внешностью и задорным характером."
    nar "Иногда я ловила себя на мысли, что немного ей завидую. У меня никогда не получилось так легко сходиться с людьми."
    alina "Ну ладно. Но только не бросай меня там одну, а то я с ума сойду от страха."
    kate "Скажешь тоже. Давай, собирайся скорее, а то все пропустим."
    scene room_gg_effect2 with Dissolve(1.0)
    scene room_gg_effect1 with Dissolve(1.0)
    scene room_gg_day with Dissolve(1.0)
    # тут снова вспышка - возвращаемся в настоящее
    nar "Телефон снова подал голос, и я вынырнула из своих воспоминаний."
    k_nvl "Эй, ну ты обиделась что ли? Извини, что так получилось. {image=images/phone/emoji/sweat.png}"
    nar "В какой-то момент на вечеринке она явно перебрала с алкоголем, а потом и вовсе исчезла."
    nar "Как мне потом сообщили ее друзья, ушла она не одна, а в компании какого-то парня."
    nar "А дальше я выскочила из квартиры и хотела вызвать такси, но телефон как назло разрядился. Я решила дойти пешком, благо жила недалеко, но опоздала на развод мостов."
    nar "Под проливным дождем и без зонта я зашла в первый же попавшийся бар, чтобы переждать непогоду. А дальше было что-то странное."
    nar "Потому что позже я проснулась на скамейке. Может, меня опоили в этом баре? Этого я не помнила."
    jump morning1_2


label morning1_2:
    menu:
        nar "Я и правда могла бы обидеться на поведение Кати, ведь она по сути бросила меня одну. С другой стороны она моя единственная здесь подруга и часто помогала мне с учебой и не только."
        "Поругаться":
            $od = od - 5
            a_nvl "Странный вопрос после того, как ты меня вчера бросила."
            k_nvl "Прости, я не специально. Поверь, у меня была причина."
            k_nvl "В универе тебе все расскажу, не обижайся."
            jump morning_more1
        "Ответить нейтрально":
            a_nvl "Да все в порядке."
            k_nvl "Извини, что вчера так получилось."
            k_nvl "В универе тебе все расскажу."
            a_nvl "Ок."
            k_nvl "Тогда не опаздывай. Первой парой вышка."
        "Рассказать все":
            $od = od + 5
            a_nvl "Я вчера опоздала на развод мостов."
            a_nvl "Домой только под утро добралась."
            a_nvl "Еще и телефон разрядился."
            k_nvl "Но с тобой все в порядке?"
            a_nvl "Вроде да..."
            k_nvl "Прости, что ушла без тебя."
            k_nvl "В универе тебе все расскажу."
            a_nvl "Ладно, буду собираться."
            k_nvl "Не опаздывай. Первой парой вышка."
    jump day1_metro

label morning_more1:
    menu:
        nar "..."
        "Высказать все обиды":
            $od = od - 5
            a_nvl "Какая еще причина? Очередной смазливый парень?"
            k_nvl "Да ну тебя! Тебе не понять!"
            a_nvl "Конечно, не понять. Я же не такая популярная, как ты."
            nar "Я была так зла на Катю, что решила высказать ей все накопившиеся обиды."
            nar "Но она как будто чувствовала это и ничего мне не ответила."
            alina_m "Что-то мне совсем не хочется ехать в универ."
            alina_m "Пожалуй пропущу сегодняшний день."
            nar "Я закинула в себя пару бутербродов и решила вздремнуть пару часов."
            nar "Очнулась я, когда часы на мобильном показывали двенадцать дня. Я проспала около четырех часов."
            alina_m "Не восемь часов, но тоже неплохо."
            call day1_dream
            jump day1_street
        "Остыть":
            a_nvl "Ладно. Проехали."
            k_nvl "Тогда не опаздывай. Первой парой вышка."
            jump day1_metro



#=======================================================================УТРО-2=======================================

label morning2_1:
    scene room_gg with fade
    nar "Обрывки сна всё ещё шевелились где-то на границе сознания, медленно затухая."
    alina_m "Ну и бред. Это всё мутные разговоры Авроры Говардовны и кофе с анисом на ночь. Хорошо, что сегодня воскресенье, и не надо ехать в универ."
    alina_m "Можно поваляться подольше. Ох, и приснится же такое."
    nar "Привычно нашарив на прикроватной тумбочке телефон, я взглянула на экран и увидела уведомление о новом сообщении."
    alina_m "Интересно, кто это пишет в такую рань?"
    nar "Впрочем, оказалось, что сообщение было отправлено поздно ночью."
    k_nvl "Алин, привет. Ты как?"
    if od < 45:
        k_nvl "Прости меня, я была неправа. Не сердись, ок?"
        nar "Я фыркнула, вспомнив вчерашний день."
        nar "Но обида уже понемногу начала утихать. Всё-таки, Катя — моя лучшая подруга, и глупо злиться, когда человек уже извинился."
    a_nvl "Странно...В двух словах и не расскажешь."
    k_nvl "Давай увидимся? Мне надо тебя кое с кем познакомить."
    k_nvl "Это очень важно для меня."
    k_nvl "Давай в нашем кафе через час?"
    if od < 45:
        a_nvl "Я что-то не очень хорошо себя чувствую. Может, простудилась вчера."
        k_nvl "Ну плиииз! Ну ради меня. Я угощу тебя лавандовым латте ;)"
        a_nvl "Ок :) С тобой проще согласиться, чем спорить."
        k_nvl "Да! Хорошо что ты это понимаешь ;)"
    else:
        a_nvl "Ну хорошо."
        k_nvl "Отлично! Жду!"
    nar " Значит, через час? Поспать уже не удастся. Потянувшись, я встала с кровати и, нашарив ногами растоптанные шлёпанцы, отправилась в ванную."
    alina_m "Интересно, что это за новый парень у Кати?"
    nar "Впрочем, с её умением нравиться людям, она вполне могла менять парней, как перчатки. Мне такое было не дано, на меня редко обращали внимание."
    nar "Едва взглянув в большое мутноватое зеркало, я дёрнулась так, что врезалась спиной в дверной косяк. В середине моего лба, где раньше я заметила пятно, похожее на синяк, вращаясь и моргая, пялился в пустоту глаз!"
    if uh >= 15:
        nar "А там, где сходятся ключицы, торчало нечто отвратительное, бурое, склизкое. Оно непрестанно шевелилось, словно пыталось ощупать всё вокруг себя."
        nar "Я судорожно взмахнула рукой, пытаясь оторвать от себя это мерзкое нечто, напоминавшее щупальце, но ладонь схватила лишь пустоту."
        nar "Ничего этого не было физически, ни щупальца, ни глаза, и всё же я ощущала пульсацию и боль в теле."
    else:
        nar "Я судорожно взмахнула рукой и попыталась дотронуться до глаза, но почувствовала только гладкую поверхность собственного лба. Третьего глаза как будто не было, но я же его видела!"
    alina_m "Что это, блин, такое?!"
    if uh >= 15:
        alina_m "Глаз? У меня на лбу глаз?"
        nar ""
    else:
        alina_m "Глаз? Щупальце?"
    alina_m "Бред какой-то... Это просто сон рекурсивный. Мне кажется, что я проснулась, но это — всего лишь сон... Я сейчас проснусь и пойду в ванную..."
    nar "Я зажмурила глаза и постаралась убедить себя в том, что нужно проснуться."
    nar "Страшась снова увидеть своё отражение, я всё же сделала шаг вперёд и открыла крепко зажмуренные глаза. Но вот ж он! Вот же третий глаз у меня во лбу — уродливый, с ярко-красной радужкой и вытянутым зрачком."
    if uh >= 15:
        nar "А вот и щупальце — бурое, цвета сырой говяжьей печени, оно едва заметно извивалось и пульсировало."
    nar "Я зажала рот руками, чтобы не закричать и привалилась спиной к двери. Наверное, я с ума сошла... и это мои глюки..."
    nar "Громкий стук в дверь прервал мои панические мысли."
    ag "Деточка, ты там надолго? Ты же знаешь, что меня с утра всегда слабит после коньяка на ночь. Возраст даёт о себе знать."
    alina "Минутку!"
    nar "Я постаралась унять дрожь в голосе, но мне это не удалось."
    nar "Когда я, наконец, открыла дверь, Аврора Говардовна переступила порог ванной и внимательно посмотрела на меня, не изменившись в лице."
    alina "Доброе утро. Извините, за глупый вопрос — как я выгляжу?"
    ag "Невыспавшейся немного... кхе-кхе. Вопрос не в том, как ты выглядишь. Вопрос в том, как ты себя ощущаешь."
    nar "С этими словами старуха попятилась и закрыла за собой дверь."
    alina_m "Вряд ли бы она реагировала так буднично, если бы у меня было бы что-то на лице..."
    alina_m "Что же делать? А если там всё-таки что-то есть? Как же я пойду в кафе в таком виде?"
    alina_m "Наощупь всё гладко. Но зеркало..."
    alina_m "Это галлюцинации? Я схожу с ума... Или сплю... или схожу с ума во сне."
    alina_m "Нет, мне нужно идти, нужно понять, что происходит, Катя поможет мне, должна помочь, она же моя подруга."
    nar "Я принялась собираться на встречу с Катей."
    nar "Выйдя из дома, я чуть ли не нос к носу столкнулась со стариком в потёртом клетчатом пальто. Он держал подмышкой крошечную собачонку в лиловом комбинезончике."
    nar "Та выпучилась на меня и затряслась всем телом, заходясь визгливым лаем."
    ded "Фердинанд, фу! Извините, сударыня."
    nar "Он цыкнул на собаку и посмотрел на меня с улыбкой, будто извиняясь."
    nar "Я с ужасом ждала, что старик побежит прочь, увидев моё лицо, но взгляд его нисколько не изменился. Собачонка продолжала надрываться, казалось, её вот-вот хватит удар."
    ded "Да что с тобой такое, Фердинанд?  Вы уж простите старика, разворчался он что-то."
    nar "Мужчина снова улыбнулся и, выговаривая что-то собаке, скрылся в подъезде."
    alina_m "Ничего... Он ничего не заметил. Не закричал, не убежал."
    nar "Всю дорогу до кафе я старалась попасться на глаза как можно большему числу прохожих, но никто из них не обращал на меня особого внимания."
    nar "Это несколько успокоило меня, и в кафе я зашла уже, почти не паникуя."
    jump cafe1

#=======================================================================УТРО-3=======================================


#=======================================================================УТРО-4=======================================